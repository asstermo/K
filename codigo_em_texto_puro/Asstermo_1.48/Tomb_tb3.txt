()
Prgm

Local  ploc,flst,li1,li2,i,i1,i2,i3,js,jb,tlista,tloc,txtlist,txttle,length,lpos,maxlist,txtlm,cm,floc,h0f

ClrIO
Disp "A preparar..."
Disp "Por Favor aguarde..."
comb_xt(23)

setMode("Display Digits","Float 12")→cm

8→maxlist
{5,15,25,35}→txtlm

1→li1
5→li2
seq(tabigp23[i,6],i,li1,li2)→flst

tabigp23[1,2]→li1
tabigp23[1,4]→li2
seq(string(tabigp23[i,1]),i,li1,li2,6)→tlista

0→floc
0→tloc
Lbl  start

ClrIO
Dialog
  Title  "Tabelas: Prop. Gases Ideais"
  Text  "---- Visualização da Tabela A-23 ----"
  Text  " Escolha o gás e a Temperatura donde "
  Text  "comeca a listar. Serão apresentadas "
  Text  "máximo 8 entradas. (Esc para sair)"
  Text  ""
  DropDown  "Qual gás?",flst,floc
  DropDown  "Temperatura (K)?",tlista,tloc
  DropDown  "Listar quantas entradas?",seq(string(i),i,1,8),maxlist
EndDlog

If  ok=0
  Goto  fim

tabigp23[floc,7]→h0f

li1+(tloc-1)*6→i1
i1+maxlist-1→i2
If i2>li2 Then
  li2→i2
EndIf

"Gás: "&flst[floc]&"; h0f = "&string(h0f)&" kJ/kmol"→txttle

ClrIO
Disp  "Tabela em elaboração"
Output  15,10,"00% "
seq("",i,1,8)→txtlist
0→lpos

(floc-1)*3+1→jb
{1,jb+1,jb+2,jb+3}→js
For  i,1,4
  lpos+1→lpos
  For  i3,i1,i2
    i3-i1+1→length
    mid(txtlist[length]&string(tabigp23[i3,js[i]])&"00000",1,txtlm[lpos])→txtlist[length]
    If  i<4
      txtlist[length]&" | "→txtlist[length]
  EndFor

  Output  15,10,string(lpos*25)&"% "
EndFor

ClrIO
DispHome

Dialog
  Title  txttle
  Text  "Esc para Sair. OK para outra Temp."
  Text  "T:K h:kJ/kmol u:kJ/kmol s:kJ/kmol.K"
  Text  txtlist[1]
  Text  txtlist[2]
  Text  txtlist[3]
  Text  txtlist[4]
  Text  txtlist[5]
  Text  txtlist[6]
  Text  txtlist[7]
  Text  txtlist[8]
EndDlog
If  ok=1
  Goto  start

Lbl  fim
setMode("Display Digits",cm)→cm

comb_td(23)

ClrIO
DispHome

EndPrgm