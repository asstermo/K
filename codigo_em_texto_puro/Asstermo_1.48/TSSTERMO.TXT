()
Prgm
REM TERMO\ASSTERMO. Assistente de Termodinamica: Menu Principal. Precisa das seguintes matrizes: TabH2OA(2-5), TabAmo(13-15), TabR22A(7-9), Tab134(10-12), TabMMol, TabAr. Data: 08-09/2004. Autor: Bruno Santos, wyldckat@sapo.pt, asstermo.no.sapo.pt
REM Versao descomprimida de nome TSSTermo

Local opcao,opcao1,opcao11,tabouval,oldfold,puop,pstr,p,t,tstr,v,u,h,s,punit,tunit,vunit,uunit,hunit,sunit,x,fgstr,fgfstr,fgval,xstr,fgop,cm,cm2,hpop,hpstr,spop,spstr,pf,hf,opcao2,opcao21,ttltxt,ttltxt2,tabnum,opcao7,arval,art,arh,aru,ars0,arpr,arvr,goon,contastr,contares,mmolop,mmunit,rmunit,mmolstr,doit,errtext,ClrGo,hstr,vstr,pfstr,GoGoGo,resulta,tmpstr,tmpstr2,tmpunit,aonoff

Define GoGoGo(OOKK,valuelst)=Prgm
Local i,i2,t

If OOKK=1 Then

  false→GoGoGoR
  Try
    dim(valuelst)→i2
    For i,1,i2
      expr(valuelst[i])→t
      If GetType(t)!="NUM"
        Exit
    EndFor
    If i=i2+1
      true→GoGoGoR

  Else
    ClrErr
  EndTry
  If not GoGoGoR
    Text "Valores inválidos!!!"
Else
  true→GoGoGoR
EndIf

EndPrgm

""→contastr
0→contares
1→mmolop
"kg/kmol"→mmunit
"kJ/kg.K"→rmunit
0→p
0→pf
0→t
0→v
0→u
0→h
0→s
0→x
0→arval
0→art
0→arh
0→aru
0→ars0
0→arpr
0→arvr
0→opcao
0→opcao1
0→opcao11
0→opcao2
0→opcao21
0→opcao7
"Valores inválidos!!!"→errtext
""→fgstr

setMode("Display Digits","Float 12")→cm
setMode("Exponential Format","NORMAL")→cm2
setFold(termo)→oldfold
"Valores"→tabouval
REM "Tabelas"→TabOuVal
"bar"→punit
"ºC"→tunit
"m^3/kg"→vunit
"kJ/kg"→uunit
"kJ/kg"→hunit
"kJ/(kg.K)"→sunit
0→puop

Lbl topo
"Topo"→goon

PopUp {"Água","Amónia","Refrigerante 22","Refrigerante 134a","Massas Molares+R's","Ass. Psicrometria","Tabela do Ar","Assist. Combustao","ASSTermo: Sair","Sobre ASSTermo","Auto Alpha Off"},opcao

If opcao=1 Then

Lbl op1
"Op1"→goon

PopUp {"Proc. Por P e/ou T","Proc. Por h e P","Proc. s e P (Tab)","Proc. s e P(L.Sat)","Proc. Por v e P","Proc. Por u e P","Água: Para Trás","ASSTermo: Sair","Fazer umas contas","Interpolações"},opcao1

  If opcao1=1 Then

    Lbl op11
    "Op11"→goon

    PopUp {"Líquido Comprimido","Líquido/Vapor Sat","Vapor Sobreaquecid","Modo(T\V): "&tabouval,"Água: Para Trás","ASSTermo: Sair","Fazer umas contas","Interpolações"},opcao11

    If opcao11=1 Then

      If tabouval="Valores" Then
        0→puop
        string(p)→pstr
        string(t)→tstr

        Loop
          Dialog
            Title "Líquido Comprimido"
            Text " Indique a pressão (e a unidade) e a"
            Text "temperatura. Receberá as propriedades"
            Text "correspondentes! Ver no ""Modo(T\V): "
            Text "Tabelas"" para conhecer os limites."
            Text ""
            Request "Pressão",pstr
            DropDown "Unidade da pressão",{punit,"MPa","kPa","Pa"},puop
            Request "Temperatura ("&tunit&")",tstr
          EndDlog

          GoGoGo(Ok,{pstr,tstr})
          If GoGoGoR
            Exit
        EndLoop
        DelVar GoGoGoR

        If ok=1 Then

          expr(pstr)→p
          expr(tstr)→t
          pconv(p,puop)→p

          ClrIO
          Disp "Iniciando cálculos"
          Disp "de v,u,h e s"
          h2o_xtc(5)

          h2o_lc(p,t,{"v","u","h","s"})→v
          h2o_tdel(5)
          v[2]→u
          v[3]→h
          v[4]→s
          v[1]→v
          Disp "Calculado"

          Dialog
            Title "Resultados: Líquido Comprimido"
            Text " Se alguma propriedade for ""undef"","
            Text "então é porque sai fora das tabelas!"
            Text ""
            Text "p= "&string(p)&" "&punit
            Text "T= "&string(t)&" "&tunit
            Text "v= "&string(v)&" "&vunit
            Text "u= "&string(u)&" "&uunit
            Text "h= "&string(h)&" "&hunit
            Text "s= "&string(s)&" "&sunit
          EndDlog

          ClrIO
        EndIf

      Else

        h2o_lct()

      EndIf

    ElseIf opcao11=2 Then

      If tabouval="Valores" Then

        string(x)→xstr

        Loop
          Dialog
            Title "Água Saturada"
            Text " Indique a pressão (em bar) ou a tem_"
            Text "peratura (em ºC) e o título(de 0 a 1)."
            Text "Receberá as propriedades corresponden_"
            Text "tes! Ver no ""Modo(T\V): Tabelas"" para"
            Text "conhecer os limites."
            Text ""
            Request "Valor Pressão\Temp",fgstr
            DropDown "Pressão\Temperatura?",{"Pressão","Temp."},fgop
            Request "Título (x, 0 a 1)",xstr
          EndDlog

          GoGoGo(Ok,{xstr,fgstr})
          If GoGoGoR
            Exit

        EndLoop
        DelVar GoGoGoR

        If ok=1 Then

          expr(xstr)→x

          ClrIO
          Disp "Iniciando cálculos"
          Disp "de p\T,v,u,h e s"

          If fgop=1 Then
            expr(fgstr)→p
            "p"→fgfstr
            p→fgval
            h2o_xtc(3)
          ElseIf fgop=2 Then
            expr(fgstr)→t
            "T"→fgfstr
            t→fgval
            h2o_xtc(2)
          EndIf

          h2o_fg(fgval,fgfstr,x,{"v","u","h","s",fgfstr})→v
          v[2]→u
          v[3]→h
          v[4]→s
          If fgop=1 Then
            v[5]→t
            h2o_tdel(3)
          ElseIf fgop=2 Then
            v[5]→p
            h2o_tdel(2)
          EndIf
          v[1]→v
          Disp "(T\p,v,u,h,s) - Calculado"

          Dialog
            Title "Resultados: Água saturada"
            Text " Se alguma propriedade for ""undef"","
            Text "então é porque sai fora das tabelas!"
            Text ""
            Text "p= "&string(p)&" "&punit
            Text "T= "&string(t)&" "&tunit
            Text "v= "&string(v)&" "&vunit
            Text "u= "&string(u)&" "&uunit
            Text "h= "&string(h)&" "&hunit
            Text "s= "&string(s)&" "&sunit
            Text "Título (x)= "&string(x)
          EndDlog

        EndIf

      Else

        h2o_fgt()

      EndIf

    ElseIf opcao11=3 Then

      If tabouval="Valores" Then

        0→puop
        string(p)→pstr
        string(t)→tstr

        Loop
          Dialog
            Title "Vapor Sobreaquecido"
            Text " Indique a pressão (e a unidade) e a"
            Text "temperatura. Receberá as propriedades"
            Text "correspondentes! Ver no ""Modo(T\V):"
            Text "Tabelas"" para conhecer os limites."
            Text ""
            Request "Pressão",pstr
            DropDown "Unidade da pressão",{punit,"MPa","kPa","Pa"},puop
            Request "Temperatura ("&tunit&")",tstr
          EndDlog

          GoGoGo(Ok,{pstr,tstr})
          If GoGoGoR
            Exit

        EndLoop
        DelVar GoGoGoR

        If ok=1 Then

          expr(pstr)→p
          expr(tstr)→t

          pconv(p,puop)→p

          ClrIO
          Disp "Iniciando cálculos"
          Disp "de v,u,h e s"

          h2o_xtc(4)

          h2o_vs(p,t,{"v","u","h","s"})→v

          h2o_tdel(4)

          v[2]→u
          v[3]→h
          v[4]→s
          v[1]→v
          Disp "Calculado"

          Dialog
            Title "Resultados: Vapor Sobreaquecido"
            Text " Se alguma propriedade for ""undef"","
            Text "então é porque sai fora das tabelas!"
            Text ""
            Text "p= "&string(p)&" "&punit
            Text "T= "&string(t)&" "&tunit
            Text "v= "&string(v)&" "&vunit
            Text "u= "&string(u)&" "&uunit
            Text "h= "&string(h)&" "&hunit
            Text "s= "&string(s)&" "&sunit
          EndDlog

        EndIf

      Else

        h2o_vst()

      EndIf

    ElseIf opcao11=4 Then
      If tabouval="Valores" Then
        "Tabelas"→tabouval
      ElseIf tabouval="Tabelas" Then
        "Valores"→tabouval
      EndIf
    ElseIf opcao11=5 Then
      Goto op1
    ElseIf opcao11=6 Then
      Goto fim
    ElseIf opcao11=7 Then
      Goto contas
    ElseIf opcao11=8 Then
      ainterpd()
    EndIf

    Goto op11

  ElseIf opcao1=2 or opcao1=3 or opcao1=5 or opcao1=6 Then

    If opcao1=2 Then
      "Água: Obter propriedades só com h e p"→ttltxt2
      "entalpia"→tmpstr
      string(h)→hstr
      hunit→tmpunit
    ElseIf opcao1=3 Then
      "Água: Obter propriedades só com s e p"→ttltxt2
      "entropia"→tmpstr
      string(s)→hstr
      sunit→tmpunit
    ElseIf opcao1=5 Then
      "Água: Obter propriedades só com v e p"→ttltxt2
      "vol.esp."→tmpstr
      string(v)→hstr
      vunit→tmpunit
    ElseIf opcao1=6 Then
      "Água: Obter propriedades só com u e p"→ttltxt2
      "e.interna"→tmpstr
      string(u)→hstr
      uunit→tmpunit
    EndIf

    string(p)→pstr
    Loop
      Dialog
        Title ttltxt2
        Text "Indique o(a) "&tmpstr&" (em "&tmpunit&"),"
        Text "a pressão (em bar) e em que parte"
        Text "do diagrama. Receberá as proprieda_"
        Text "des correspondentes!"
        Text ""
        Request "Valor "&tmpstr,hstr
        Request "Valor pressão",pstr
        DropDown "Em que parte?",{"Líquido Comp.","Água Saturada","Vapor Sobreaq"},hpop
      EndDlog

      GoGoGo(Ok,{hstr,pstr})
      If GoGoGoR
        Exit

    EndLoop
    DelVar GoGoGoR

    If ok=1 Then

      expr(pstr)→p

      ClrIO
      Disp "Iniciando cálculos"
      Disp "Por Favor Aguarde..."

      If hpop=1 Then
        "lc"→hpstr
        h2o_xtc(5)
      ElseIf hpop=2 Then
        "fg"→hpstr
        h2o_xtc(3)
      ElseIf hpop=3 Then
        "vs"→hpstr
        h2o_xtc(4)
      EndIf

      If opcao1=2 Then
        expr(hstr)→h
        h2o_hsp(h,"h",p,hpstr,{"T","u","v","s"})→resulta
        resulta[2]→u
        resulta[3]→v
        resulta[4]→s
      ElseIf opcao1=3 Then
        expr(hstr)→s
        h2o_hsp(s,"s",p,hpstr,{"T","u","v","h"})→resulta
        resulta[2]→u
        resulta[3]→v
        resulta[4]→h
      ElseIf opcao1=5 Then
        expr(hstr)→v
        h2o_hsp(v,"v",p,hpstr,{"T","u","h","s"})→resulta
        resulta[2]→u
        resulta[3]→h
        resulta[4]→s
      ElseIf opcao1=6 Then
        expr(hstr)→u
        h2o_hsp(u,"u",p,hpstr,{"T","h","v","s"})→resulta
        resulta[2]→h
        resulta[3]→v
        resulta[4]→s
      EndIf
      resulta[1]→t

      If hpop=2 Then
        resulta[5]→x
        h2o_tdel(3)
      Else
        "Não aplicável"→x
        h2o_tdel(4)
        h2o_tdel(5)
      EndIf
      Disp "Valores Calculados"

      If hpop=1 Then
        "Resultados em Líquido Comprimido"→hpstr
      ElseIf hpop=2 Then
        "Resultados em Água Saturada"→hpstr
      ElseIf hpop=3 Then
        "Resultados em Vapor SobreAquecido"→hpstr
      EndIf

      Dialog
        Title hpstr
        Text " Se alguma propriedade for ""undef"","
        Text "então é porque sai fora das tabelas!"
        Text ttltxt2
        Text "T= "&string(t)&" "&tunit
        Text "p= "&string(p)&" "&punit
        Text "v= "&string(v)&" "&vunit
        Text "u= "&string(u)&" "&uunit
        Text "h= "&string(h)&" "&hunit
        Text "s= "&string(s)&" "&sunit
        Text "Título (x)= "&string(x)
      EndDlog

    EndIf

  ElseIf opcao1=4 Then

    string(h)→hstr
    string(v)→vstr
    string(p)→pstr
    string(pf)→pfstr
    Loop
      Dialog
        Title "Obter h final com h inicial, v e p"
        Text "Evolução (liq. comp.) a entropia cons_"
        Text "tante: Indique a entalpia inicial (em "
        Text "kJ/kg), o volume específico e as pres_"
        Text "sões (inicial e final, em bar). Rece_"
        Text "berá a entalpia final!"
        Request "Entalpia inicial",hstr
        Request "Volume específico",vstr
        Request "Pressão inicial",pstr
        Request "Pressão final",pfstr
      EndDlog

      GoGoGo(Ok,{hstr,vstr,pstr,pfstr})
      If GoGoGoR
        Exit

    EndLoop
    DelVar GoGoGoR

    If ok=1 Then

      expr(hstr)→h
      expr(vstr)→v
      expr(pstr)→p
      expr(pfstr)→pf

      h2o_hvp(h,v,p,pf)→hf

      Dialog
        Title "Resultado (líq. comp.) entropia cte."
        Text "A conta executada: hi+v*(pf-pi)→hf"
        Text "O resultado:"
        Text ""
        Text "hi= "&string(h)&" "&hunit
        Text "v= "&string(v)&" "&vunit
        Text "pi= "&string(p)&" "&punit
        Text "pf= "&string(pf)&" "&punit
        Text ""
        Text "hf= "&string(hf)&" "&hunit
      EndDlog

    EndIf

  ElseIf opcao1=7 Then
    Goto topo
  ElseIf opcao1=8 Then
    Goto fim
  ElseIf opcao1=9 Then
    Goto contas
  ElseIf opcao1=10 Then
    ainterpd()
  EndIf

  Goto op1

ElseIf opcao=2 or opcao=3 or opcao=4 Then

opcao-1→tabnum

Lbl op2
"Op2"→goon

If opcao=2 Then
  "Amónia"→ttltxt
ElseIf opcao=3 Then
  "R22"→ttltxt
ElseIf opcao=4 Then
  "R134a"→ttltxt
EndIf
PopUp {"Proc. Por P e/ou T","Proc. Por h e P","Proc. s e P (Tab)","Proc. s e P(L.Sat)","Proc. Por v e P","Proc. Por u e P",ttltxt&": Para Trás","ASSTermo: Sair","Fazer umas contas","Interpolações"},opcao2

  If opcao2=1 Then

    Lbl op21
    "Op21"→goon

    If opcao=2 Then
      "Amónia"→ttltxt
    ElseIf opcao=3 Then
      "R22"→ttltxt
    ElseIf opcao=4 Then
      "R134a"→ttltxt
    EndIf

    PopUp {"Líquido/Vapor Sat","Vapor Sobreaquecid","Modo(T\V): "&tabouval,ttltxt&": Para Trás","ASSTermo: Sair","Fazer umas contas","Interpolações"},opcao21

    If opcao21=1 Then

      If tabouval="Valores" Then

        string(x)→xstr

        If opcao=2 Then
          "Amónia Saturada"→ttltxt
        ElseIf opcao=3 Then
          "Refrigerante 22 Saturado"→ttltxt
        ElseIf opcao=4 Then
          "Refrigerante 134a Saturado"→ttltxt
        EndIf

        Loop
          Dialog
            Title ttltxt
            Text " Indique a pressão (em bar) ou a tem_"
            Text "peratura (em ºC) e o título(de 0 a 1)."
            Text "Receberá as propriedades corresponden_"
            Text "tes! Ver no ""Modo(T\V): Tabelas"" para "
            Text "conhecer os limites."
            Text ""
            Request "Valor Pressão\Temp",fgstr
            DropDown "Pressão\Temperatura?",{"Pressão","Temp."},fgop
            Request "título (x, 0 a 1)",xstr
          EndDlog

          GoGoGo(Ok,{xstr,fgstr})
          If GoGoGoR
            Exit

        EndLoop
        DelVar GoGoGoR

        If ok=1 Then

          expr(xstr)→x

          ClrIO
          Disp "Iniciando cálculos"
          Disp "de p\T,v,u,h e s"

          If fgop=1 Then
            expr(fgstr)→p
            "p"→fgfstr
            p→fgval
            refs_xtc(tabnum,2)
          ElseIf fgop=2 Then
            expr(fgstr)→t
            "T"→fgfstr
            t→fgval
            refs_xtc(tabnum,1)
          EndIf

          refs_fg(fgval,fgfstr,x,{"v","u","h","s",fgfstr},tabnum)→v
          v[2]→u
          v[3]→h
          v[4]→s
          If fgop=1 Then
            v[5]→t
            refs_tdl(tabnum,2)
          ElseIf fgop=2 Then
            v[5]→p
            refs_tdl(tabnum,1)
          EndIf
          v[1]→v
          Disp "(T\p,v,u,h,s) - Calculado"

          If opcao=2 Then
            "Resultados: Amónia Saturada"→ttltxt
          ElseIf opcao=3 Then
            "Resultados: R22 Saturado"→ttltxt
          ElseIf opcao=4 Then
            "Resultados: R134a Saturado"→ttltxt
          EndIf

          Dialog
            Title ttltxt
            Text " Se alguma propriedade for ""undef"","
            Text "então é porque sai fora das tabelas!"
            Text ""
            Text "p= "&string(p)&" "&punit
            Text "T= "&string(t)&" "&tunit
            Text "v= "&string(v)&" "&vunit
            Text "u= "&string(u)&" "&uunit
            Text "h= "&string(h)&" "&hunit
            Text "s= "&string(s)&" "&sunit
            Text "título (x)= "&string(x)
          EndDlog

          ClrIO
        EndIf

      Else

        refs_fgt(tabnum)

      EndIf

    ElseIf opcao21=2 Then

      If tabouval="Valores" Then

        0→puop
        string(p)→pstr
        string(t)→tstr

        If opcao=2 Then
          "Amónia"→ttltxt
        ElseIf opcao=3 Then
          "R22"→ttltxt
        ElseIf opcao=4 Then
          "R134a"→ttltxt
        EndIf

        Loop
          Dialog
            Title ttltxt&": Vapor Sobreaquecido"
            Text " Indique a pressão (e a unidade) e a"
            Text "temperatura. Receberá as propriedades"
            Text "correspondentes! Ver no ""Modo(T\V):"
            Text "Tabelas"" para conhecer os limites."
            Text ""
            Request "Pressão",pstr
            DropDown "Unidade da pressão",{punit,"MPa","kPa","Pa"},puop
            Request "Temperatura ("&tunit&")",tstr
          EndDlog

          GoGoGo(Ok,{pstr,tstr})
          If GoGoGoR
            Exit

        EndLoop
        DelVar GoGoGoR

        If ok=1 Then

          expr(pstr)→p
          expr(tstr)→t

          pconv(p,puop)→p

          ClrIO
          Disp "Iniciando cálculos"
          Disp "de v,u,h e s"
          refs_xtc(tabnum,3)
          refs_vs(p,t,{"v","u","h","s"},tabnum)→v

          refs_tdl(tabnum,3)

          v[2]→u
          v[3]→h
          v[4]→s
          v[1]→v
          Disp "Calculado"

          Dialog
            Title ttltxt&": Resultados Vapor Sobreaq"
            Text " Se alguma propriedade for ""undef"","
            Text "então é porque sai fora das tabelas!"
            Text ""
            Text "Resultados:"
            Text "p= "&string(p)&" "&punit
            Text "T= "&string(t)&" "&tunit
            Text "v= "&string(v)&" "&vunit
            Text "u= "&string(u)&" "&uunit
            Text "h= "&string(h)&" "&hunit
            Text "s= "&string(s)&" "&sunit
          EndDlog

          ClrIO
        EndIf

      Else

        refs_vst(tabnum)

      EndIf

    ElseIf opcao21=3 Then
      If tabouval="Valores" Then
        "Tabelas"→tabouval
      ElseIf tabouval="Tabelas" Then
        "Valores"→tabouval
      EndIf
    ElseIf opcao21=4 Then
      Goto op2
    ElseIf opcao21=5 Then
      Goto fim
    ElseIf opcao21=6 Then
      Goto contas
    ElseIf opcao21=7 Then
      ainterpd()
    EndIf

    Goto op21

  ElseIf opcao2=2 or opcao2=3 or opcao2=5 or opcao2=6 Then

    If opcao=2 Then
      "Amónia"→ttltxt
    ElseIf opcao=3 Then
      "Refrigerante 22"→ttltxt
    ElseIf opcao=4 Then
      "Refrigerante 134a"→ttltxt
    EndIf

    If opcao2=2 Then
      "-- Obter propriedades só com h e p --"→ttltxt2
      "entalpia"→tmpstr
      string(h)→hstr
      hunit→tmpunit
    ElseIf opcao2=3 Then
      "-- Obter propriedades só com s e p --"→ttltxt2
      "entropia"→tmpstr
      string(s)→hstr
      sunit→tmpunit
    ElseIf opcao2=5 Then
      "-- Obter propriedades só com v e p --"→ttltxt2
      "vol.esp."→tmpstr
      string(v)→hstr
      vunit→tmpunit
    ElseIf opcao2=6 Then
      "-- Obter propriedades só com u e p --"→ttltxt2
      "e.interna"→tmpstr
      string(u)→hstr
      uunit→tmpunit
    EndIf

    string(p)→pstr
    Loop
      Dialog
        Title ttltxt
        Text ttltxt2
        Text ""
        Text "Indique o(a) "&tmpstr&" (em "&tmpunit&"),"
        Text "a pressão (em bar) e em que parte"
        Text "do diagrama. Receberá as proprieda_"
        Text "des correspondentes!"
        Request "Valor "&tmpstr,hstr
        Request "Valor pressão",pstr
        DropDown "Em que parte?",{"Liq./Vap. Sat.","Vapor Sobreaq"},hpop
      EndDlog

      GoGoGo(Ok,{hstr,pstr})
      If GoGoGoR
        Exit

    EndLoop
    DelVar GoGoGoR

    If ok=1 Then

      expr(pstr)→p

      ClrIO
      Disp "Iniciando cálculos"
      Disp "Por Favor Aguarde..."

      If hpop=1 Then
        "fg"→hpstr
        refs_xtc(tabnum,2)
      ElseIf hpop=2 Then
        "vs"→hpstr
        refs_xtc(tabnum,3)
      EndIf

      If opcao2=2 Then
        expr(hstr)→h
        refs_hsp(h,"h",p,hpstr,{"T","u","v","s"},tabnum)→resulta
        resulta[2]→u
        resulta[3]→v
        resulta[4]→s
      ElseIf opcao2=3 Then
        expr(hstr)→s
        refs_hsp(s,"s",p,hpstr,{"T","u","v","h"},tabnum)→resulta
        resulta[2]→u
        resulta[3]→v
        resulta[4]→h
      ElseIf opcao2=5 Then
        expr(hstr)→v
        refs_hsp(v,"v",p,hpstr,{"T","u","h","s"},tabnum)→resulta
        resulta[2]→u
        resulta[3]→h
        resulta[4]→s
      ElseIf opcao2=6 Then
        expr(hstr)→u
        refs_hsp(u,"u",p,hpstr,{"T","h","v","s"},tabnum)→resulta
        resulta[2]→h
        resulta[3]→v
        resulta[4]→s
      EndIf
      resulta[1]→t
      If hpop=1 Then
        refs_tdl(tabnum,2)
        resulta[5]→x
      Else
        refs_tdl(tabnum,3)
        "Não aplicável"→x
      EndIf
      Disp "Valores Calculados"

      If hpop=1 Then
        "Líq./Vap. Saturado"→hpstr
      ElseIf hpop=2 Then
        "Vapor SobreAquecido"→hpstr
      EndIf

      If opcao=2 Then
        hpstr&" (Amónia)"→hpstr
      ElseIf opcao=3 Then
        hpstr&" (R22)"→hpstr
      ElseIf opcao=4 Then
        hpstr&" (R134a)"→hpstr
      EndIf

      Dialog
        Title hpstr
        Text ttltxt2
        Text " Se alguma propriedade for ""undef"","
        Text "então é porque sai fora das tabelas!"
        Text "T= "&string(t)&" "&tunit
        Text "p= "&string(p)&" "&punit
        Text "v= "&string(v)&" "&vunit
        Text "u= "&string(u)&" "&uunit
        Text "h= "&string(h)&" "&hunit
        Text "s= "&string(s)&" "&sunit
        Text "título (x)= "&string(x)
      EndDlog

      ClrIO
    EndIf

  ElseIf opcao2=4 Then

    If opcao=2 Then
      "Amónia"→ttltxt
    ElseIf opcao=3 Then
      "R22"→ttltxt
    ElseIf opcao=4 Then
      "R134a"→ttltxt
    EndIf

    string(h)→hstr
    string(v)→vstr
    string(p)→pstr
    string(pf)→pfstr
    Loop
      Dialog
        Title ttltxt&": obter hf com hi, v e p"
        Text "Evolução (liq. comp.) a entropia cons_"
        Text "tante: Indique a entalpia inicial (em"
        Text "kJ/kg), o volume específico e as pres_"
        Text "soes (inicial e final, em bar). Rece_"
        Text "bera a entalpia final!"
        Request "Entalpia inicial",hstr
        Request "Volume específico",vstr
        Request "Pressão inicial",pstr
        Request "Pressão final",pfstr
      EndDlog

      GoGoGo(Ok,{hstr,vstr,pstr,pfstr})
      If GoGoGoR
        Exit

    EndLoop
    DelVar GoGoGoR

    If ok=1 Then

      expr(hstr)→h
      expr(vstr)→v
      expr(pstr)→p
      expr(pfstr)→pf

      refs_hvp(h,v,p,pf)→hf

      Dialog
        Title ttltxt
        Text "Resultado (liq. comp.) entropia cons_"
        Text "tante... A conta executada:"
        Text "hi+v*(pf-pi)→hf. O resultado:"
        Text ""
        Text "hi= "&string(h)&" "&hunit
        Text "v= "&string(v)&" "&vunit
        Text "pi= "&string(p)&" "&punit
        Text "pf= "&string(pf)&" "&punit
        Text ""
        Text "hf= "&string(hf)&" "&hunit
      EndDlog

    EndIf

  ElseIf opcao2=7 Then
    Goto topo
  ElseIf opcao2=8 Then
    Goto fim
  ElseIf opcao2=9 Then
    Goto contas
  ElseIf opcao2=10 Then
    ainterpd()
  EndIf

  Goto op2

ElseIf opcao=5 Then

mol_xt(1)

seq(mid(tabmmol[i,1],1,16),i,1,27)→mmolstr

Dialog
  Title "Listagem das massas molares e Rs"
  Text " Escolha a substância da lista, para a"
  Text "qual pretende ver a informação."
  Text ""
  DropDown "Substância",mmolstr,mmolop
EndDlog

If  ok=0
  Goto mmolfim

Lbl mmolint

Dialog
  Title "Informação da substância..."
  Text "Para a substância '"&tabmmol[mmolop,1]&"':"
  Text ""
  Text "Fórmula química = "&tabmmol[mmolop,2]
  Text "Massa molar = "&string(tabmmol[mmolop,3])&" "&mmunit
  Text "R mássico = "&string(tabmmol[mmolop,4])&" "&rmunit
  Text ""
  Text "Ok para ver outra, Esc para sair:"
  DropDown "Substância",mmolstr,mmolop
EndDlog

If  ok=1
  Goto mmolint

Lbl mmolfim

0→mmolstr
mol_xt(0)

ElseIf opcao=6 Then

  asspsicr()

ElseIf opcao=7 Then

Lbl op7
"Op7"→goon

PopUp {"Procurar por T","Procurar por h","Procurar por u","Procurar por s0","Procurar por pr","Procurar por vr","Ver Tabela","Ar: Para Trás","ASSTermo: Sair","Fazer umas contas","Interpolações"},opcao7
  If opcao71 and opcao76 Then

    If opcao7=1 Then
      "Temperatura (K)"→ttltxt
      string(art)→arval
    ElseIf opcao7=2 Then
      "Entalpia (kJ/kg)"→ttltxt
      string(arh)→arval
    ElseIf opcao7=3 Then
      "E. Interna (kJ/kg)"→ttltxt
      string(aru)→arval
    ElseIf opcao7=4 Then
      "Entrop.0 (kJ/kg.K)"→ttltxt
      string(ars0)→arval
    ElseIf opcao7=5 Then
      "Razão de Pressão"→ttltxt
      string(arpr)→arval
    ElseIf opcao7=6 Then
      "Razão de Volume"→ttltxt
      string(arvr)→arval
    EndIf

    Loop
      Dialog
        Title "Procurar por "&ttltxt
        Text " Indique o valor da "&ttltxt&""
        Text "para a(o) qual pretende obter informa_"
        Text "ção."
        Text ""
        Request ttltxt,arval
      EndDlog

      GoGoGo(Ok,{arval})
      If GoGoGoR
        Exit

    EndLoop
    DelVar GoGoGoR

    If ok=1 Then

      o2n_xt(1)

      ClrIO
      Disp "Iniciando cálculos"
      Disp "Por Favor Aguarde..."

      If opcao7=1 Then
        expr(arval)→art
        o2n_prop(art,"T",{"h","u","s0","pr","vr"})→arval
        arval[1]→arh
        arval[2]→aru
        arval[3]→ars0
        arval[4]→arpr
        arval[5]→arvr
      ElseIf opcao7=2 Then
        expr(arval)→arh
        o2n_prop(arh,"h",{"T","u","s0","pr","vr"})→arval
        arval[1]→art
        arval[2]→aru
        arval[3]→ars0
        arval[4]→arpr
        arval[5]→arvr
      ElseIf opcao7=3 Then
        expr(arval)→aru
        o2n_prop(aru,"u",{"T","h","s0","pr","vr"})→arval
        arval[1]→art
        arval[2]→arh
        arval[3]→ars0
        arval[4]→arpr
        arval[5]→arvr
      ElseIf opcao7=4 Then
        expr(arval)→ars0
        o2n_prop(ars0,"s0",{"T","h","u","pr","vr"})→arval
        arval[1]→art
        arval[2]→arh
        arval[3]→aru
        arval[4]→arpr
        arval[5]→arvr
      ElseIf opcao7=5 Then
        expr(arval)→arpr
        o2n_prop(arpr,"pr",{"T","h","u","s0","vr"})→arval
        arval[1]→art
        arval[2]→arh
        arval[3]→aru
        arval[4]→ars0
        arval[5]→arvr
      ElseIf opcao7=6 Then
        expr(arval)→arvr
        o2n_prop(arvr,"vr",{"T","h","u","s0","pr"})→arval
        arval[1]→art
        arval[2]→arh
        arval[3]→aru
        arval[4]→ars0
        arval[5]→arpr
      EndIf

      o2n_xt(0)
      Disp "Valores Calculados"

      Dialog
        Title "Procura por "&ttltxt
        Text " Os resultados obtidos foram (undef"
        Text "significa fora da tabela):"
        Text "Temperatura (K)= "&string(art)
        Text "Entalpia (kJ/kg)= "&string(arh)
        Text "E. Interna (kJ/kg)= "&string(aru)
        Text "Entrop.0 (kJ/(kg.K))= "&string(ars0)
        Text "Razão de Pressão = "&string(arpr)
        Text "Razão de Volume = "&string(arvr)
      EndDlog

      ClrIO
    EndIf

  ElseIf opcao7=7 Then

    o2n_prpt()

  ElseIf opcao7=8 Then
    Goto topo
  ElseIf opcao7=9 Then
    Goto fim
  ElseIf opcao7=10 Then
    Goto contas
  ElseIf opcao7=11 Then
    ainterpd()
  EndIf

  Goto op7

ElseIf opcao=8 Then

  asscomb()

ElseIf opcao=9 Then

  Goto fim

ElseIf opcao=10 Then

  Dialog
    Title "Sobre o Assistente de Termodinâmica"
    Text "  Assistente de Termodinâmica v1.48"
    Text ""
    Text "Agosto-Setembro, Novembro de 2004"
    Text "      Julho de 2005"
    Text "2004-5 (C) Bruno M. S. Santos"
    Text ""
    Text "Email: wyldckat@sapo.pt"
    Text "Página na net: asstermo.no.sapo.pt"
    Text "O manual está disponivel na página!"
  EndDlog

ElseIf opcao=11 Then

  ClrIO
  Disp "Kevin Kofler é o autor"
  Disp "do Auto Alpha Off e do"
  Disp "Event Hook Uninstaller."

  2→aonoff
  Dialog
    Title "Tratar daquilo que chateia..."
    Text " Auto Alpha Off é um mini programa que"
    Text "trata de nas caixas de diálogo começar"
    Text "logo a escrever em números em vez de"
    Text "letras. MAS, isto é só para as TI-89,"
    Text "e usem a vosso próprio risco... mas em"
    Text "principio é seguro! Uma vez activado,"
    Text "fica sempre activado!"
    DropDown "Auto Alpha Off",{"Activar","Desactivar"},aonoff
  EndDlog

  If Ok=1 Then
    If aonoff=1 Then
      autoaoff()
    Else
      Disp "Escolha o Auto Alpha Off"
      uninevhk()
    EndIf
  EndIf

  ClrIO

EndIf

Goto topo

Lbl contas

Dialog
  Title "Faça aqui as suas contas"
  Text " Escreva a conta que pretende fazer."
  Text "Utilizar apenas expressões simples."
  Text "Nada de ""solve"", "":"" ou coisas do gé_"
  Text "nero. Mas Funcões internas são permi_"
  Text "tidas, como: approx, sin, cos :)"
  Text ""
  Request "A expressão",contastr
EndDlog

If ok=1 Then
Try
expr(contastr)→contares
 Else
"Expressão inválida!!!"→contares
ClrErr
EndTry

string(contares)→contares

Dialog
  Title "Resultado das suas contas"
  Text "Esta é a expressão introduzida:"
  Text mid(contastr,1,42)
  Text mid(contastr,43,84)
  Text mid(contastr,85,126)
  Text "Este é o resultado:"
  Text mid(contares,1,42)
  Text mid(contares,43,84)
  Text mid(contares,85,126)
  Text "Ok para passar o resultado p/a expres_"
  Text "são inicial, Esc p/ deixar a original."
EndDlog
If  ok=1
  contares→contastr
EndIf

Goto #goon

Lbl fim
setFold(#oldfold)
setMode("Display Digits",cm)→cm
setMode("Exponential Format",cm2)→cm2
ClrIO
DispHome

EndPrgm