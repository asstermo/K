(TabNum)
Prgm

Local ploc,plista,a2li1,a2li2,a3li1,a3li2,i,i1,i2,i3,taloc,tlista,tloc,txtlist,txttle,length,lpos,maxlist,fgop,propop,tmpval,txtlm,txtlist2,cm,ttltxt,mltpr,tabela,tabelas

Refs_XTc(TabNum,1)
Refs_XTc(TabNum,2)

ClrIO
Disp  "Por favor aguarde..."

setmode("Display Digits","Float 12")»cm
8»maxlist

If TabNum=1 Then
  {"TabAmo13","TabAmo14"}»tabelas
ElseIf TabNum=2 Then
  {"TabR22A7","TabR22A8"}»tabelas
ElseIf TabNum=3 Then
  {"Tab13410","Tab13411"}»tabelas
EndIf
#(tabelas[1])[2,1]»a2li1
#(tabelas[1])[4,1]»a2li2
seq(#(tabelas[1])[1,i],i,a2li1,a2li2)»tlista
#(tabelas[2])[2,1]»a3li1
#(tabelas[2])[4,1]»a3li2
seq(#(tabelas[2])[1,i],i,a3li1,a3li2)»plista

Lbl Start

ClrIO
Disp  "Por favor aguarde..."

If TabNum=1 Then
  "Amónia Saturada (A-13 ou 14)"»ttltxt
ElseIf TabNum=2 Then
  "R22 Saturado (A-7 ou 8)"»ttltxt
ElseIf TabNum=3 Then
  "R134a Saturado (A-10 ou 11)"»ttltxt
EndIf

Dialog
  Title "Tabelas: "&ttltxt
  Text " Escolha se quer a Pressão ou a Tempe_"
  Text "ratura, o valor por onde começar,"
  Text "quantas entradas listar (máx 8) e qual"
  Text "propriedade deseja ver!"
  Text ""
  DropDown "Pressão\Temperatura?",{"Pressão","Temp."},fgop
  DropDown "Pressão (bar)?",seq(string(plista[i]),i,1,a3li2-1),ploc
  DropDown "Temperatura (¬C)?",seq(string(tlista[i]),i,1,a2li2-1),tloc
  DropDown "Listar quantas entradas?",seq(string(i),i,1,8),maxlist
  DropDown "Listar qual propriedade?",{"v","u","h","s"},propop
EndDlog

If Ok=0 
  Goto Fim

If fgop=1 Then

  a3li2-a3li1»i2
  If i2-ploc+1<maxlist Then
    i2-ploc+1»i2
  Else
    maxlist»i2
    i2-1»i2
  EndIf

Else

  a2li2-a2li1»i2
  If i2-tloc+1<maxlist Then
    i2-tloc+1»i2
  Else
    maxlist»i2
    i2-1»i2
  EndIf

EndIf

If fgop=1 Then
  a3li1+ploc-1»i1
  tabelas[2]»tabela
Else
  a2li1+tloc-1»i1
  tabelas[1]»tabela
EndIf

i1+i2»i2

{6,14,0,0,0}»txtlm

ClrIO
Disp "Tabela em elaboração"
OutPut 15,10,"00% "
seq("",i,1,8)»txtlist2
0»lpos

For i,1,2
  lpos+1»lpos
  For i3,i1,i2
    i3-i1+1»length

    #tabela[i,i3]»tmpval

    mid(txtlist2[length]&string(tmpval)&"00000",1,txtlm[lpos])»txtlist2[length]
    txtlist2[length]&"| "»txtlist2[length]
  EndFor

  OutPut 15,10,string(lpos*25)&"% "
EndFor

Lbl Loading

If fgop=1 Then
  "p (bar)| T (¬C) | "»txttle
Else
  "T (¬C)| p (bar) | "»txttle
EndIf

txtlist2»txtlist

25»mltpr
If propop=1 Then
  txttle&"vf     | vg (m3/kg)"»txttle
  {3,4}»taloc
  txtlm[2]+10»txtlm[3]
  txtlm[3]+10»txtlm[4]
ElseIf propop=2 Then
  txttle&"uf     | ug (kJ/kg)"»txttle
  {5,6}»taloc
  txtlm[2]+8»txtlm[3]
  txtlm[3]+8»txtlm[4]
ElseIf propop=3 Then
  txttle&"hf  |hfg  |hg(kJ/kg)"»txttle
  {7,9}»taloc
  txtlm[2]+8»txtlm[3]
  txtlm[3]+8»txtlm[4]
  txtlm[4]+8»txtlm[5]
20»mltpr
ElseIf propop=4 Then
  txttle&"sf  | sg (kJ/(kg.K))"»txttle
  {10,11}»taloc
  txtlm[2]+8»txtlm[3]
  txtlm[3]+8»txtlm[4]
EndIf

ClrIO
Disp "Tabela em elaboração"
OutPut 15,10,"50% "

2»lpos
For i,taloc[1],taloc[2]
  lpos+1»lpos
  For i3,i1,i2
    i3-i1+1»length

    #tabela[i,i3]»tmpval
    
    mid(txtlist[length]&string(tmpval)&"00000",1,txtlm[lpos])»txtlist[length]
    If i<taloc[2]
      txtlist[length]&"| "»txtlist[length]
  EndFor

  OutPut 15,10,string(lpos*mltpr)&"% "
EndFor

ClrIO
DispHome

Dialog
  Title "Esc p/trás. OK p/outro v,u,h,s."
  Text txttle
  Text txtlist[1]
  Text txtlist[2]
  Text txtlist[3]
  Text txtlist[4]
  Text txtlist[5]
  Text txtlist[6]
  Text txtlist[7]
  Text txtlist[8]
  DropDown "Listar qual propriedade?",{"v","u","h","s"},propop
EndDlog
If Ok=1 Then
  Goto Loading
Else
  Goto Start
EndIf

Lbl Fim
setmode("Display Digits",cm)»cm

Refs_Tdl(TabNum,1)
Refs_Tdl(TabNum,2)

ClrIO
DispHome

EndPrgm