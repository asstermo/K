(i1,i2,j1,j2,baseprop)
Func

Local  prop,i,a,b,lin1,col1,lin2,col2,inicio,fim,meio,val1,val2,val3,dj2

0→a
0→b

i1→inicio
i2→fim
tab10k27[inicio,j1]→val1
tab10k27[fim,j1]→val3

Loop

  ceiling((inicio+fim)/2)→meio

  tab10k27[meio,j1]→val2

  If baseprop<val1 or baseprop>val3 Then
    Exit
  ElseIf baseprop=val1 Then
    inicio→a
    Exit
  ElseIf baseprop=val2 Then
    meio→a
    Exit
  ElseIf baseprop=val3 Then
    fim→a
    Exit
  ElseIf fim-inicio=1 Then
    inicio→a
    Exit
  ElseIf val1<=baseprop and baseprop<=val2 Then
    meio→fim
    val2→val3
  ElseIf val2<=baseprop and baseprop<=val3 Then
    meio→inicio
    val2→val1
  EndIf

EndLoop

If a!=0 Then
 For  i,a,i2
  If baseprop<=tab10k27[i,j1] Then
    i→b
    Exit
  EndIf
 EndFor
EndIf

dim(j2)→dj2
If a=0 or b=0 Then
  undef+0*j2→prop
ElseIf a=b Then
  For  i,1,dj2
    If j2[i]!=0 Then
      tab10k27[a,j2[i]]→prop[i]
    Else
      undef→prop[i]
    EndIf
  EndFor
Else
  tab10k27[a,j1]→lin1
  tab10k27[b,j1]→lin2
  For  i,1,dj2
    If j2[i]!=0 Then
      tab10k27[a,j2[i]]→col1
      tab10k27[b,j2[i]]→col2
      interpol(lin1,col1,lin2,col2,baseprop)→prop[i]
    Else
      undef→prop[i]
    EndIf
  EndFor
EndIf

Return  prop
EndFunc
