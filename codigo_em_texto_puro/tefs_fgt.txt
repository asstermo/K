(TabNum)
Prgm

Local ploc,plista,a2li1,a2li2,a3li1,a3li2,i,i1,i2,i3,taloc,tlista,tloc,txtlist,txttle,length,lpos,maxlist,fgop,propop,tmpval,txtlm,txtfmt,cm,ttltxt,tabela,tabelas,proplist,nument,fsz,jmax,ttltxtm,fszs,jmaxs

ClrIO
Disp "A iniciar..."
eliminar("resgener")
{"v","u","h","s"}→proplist
seq(string(i),i,1,8)→nument

Refs_XTc(TabNum,1)
Refs_XTc(TabNum,2)

Disp "15%"
setmode("Display Digits","Float 12")→cm
8→maxlist

If TabNum=1 Then
  {"tabamo13","tabamo14"}→tabelas
  {3984,4094}→fszs
  {36,37}→jmaxs
ElseIf TabNum=2 Then
  {"tabr22a7","tabr22a8"}→tabelas
  {4204,4204}→fszs
  {38,38}→jmaxs
ElseIf TabNum=3 Then
  {"tab13410","tab13411"}→tabelas
  {4314,2994}→fszs
  {39,27}→jmaxs
ElseIf TabNum=4 Then
  {"tabpro16","tabpro17"}→tabelas
  {3874,3764}→fszs
  {35,34}→jmaxs
EndIf

exact(#(tabelas[1])[2,1])→a2li1
exact(#(tabelas[1])[4,1])→a2li2
combof(3,a2li1,a2li2,1,{1},{0},{""},tabelas[1],fszs[1],jmaxs[1],0)
resgener→tlista

Disp "50%"

exact(#(tabelas[2])[2,1])→a3li1
exact(#(tabelas[2])[4,1])→a3li2
combof(3,a3li1,a3li2,1,{1},{0},{""},tabelas[2],fszs[2],jmaxs[2],0)
resgener→plista

If TabNum=1 Then
  "Amónia Saturada (A-13 ou 14)"→ttltxtm
ElseIf TabNum=2 Then
  "R22 Saturado (A-7 ou 8)"→ttltxtm
ElseIf TabNum=3 Then
  "R134a Saturado (A-10 ou 11)"→ttltxtm
ElseIf TabNum=4 Then
  "Propano Saturado (A-16 ou 17)"→ttltxtm
EndIf

ClrIO

Lbl Start

Dialog
  Title "Tabelas: "&ttltxtm
  Text " Escolha se quer a Pressão ou a Tempe_"
  Text "ratura, o valor por onde começar,"
  Text "quantas entradas listar (máx 8) e qual"
  Text "propriedade deseja ver!"
  Text ""
  DropDown "Pressão\Temperatura?",{"Pressão","Temp."},fgop
  DropDown "Pressão (bar)?",plista,ploc
  DropDown "Temperatura (ºC)?",tlista,tloc
  DropDown "Listar quantas entradas?",nument,maxlist
  DropDown "Listar qual propriedade?",proplist,propop
EndDlog

If Ok=0 
  Goto Fim

If fgop=1 Then

  a3li2-a3li1→i2
  If i2-ploc+1<maxlist Then
    i2-ploc+1→i2
  Else
    maxlist→i2
    i2-1→i2
  EndIf

Else

  a2li2-a2li1→i2
  If i2-tloc+1<maxlist Then
    i2-tloc+1→i2
  Else
    maxlist→i2
    i2-1→i2
  EndIf

EndIf

If fgop=1 Then
  a3li1+ploc-1→i1
  tabelas[2]→tabela
  fszs[2]→fsz
  jmaxs[2]→jmax
Else
  a2li1+tloc-1→i1
  tabelas[1]→tabela
  fszs[1]→fsz
  jmaxs[1]→jmax
EndIf

i1+i2→i2

Lbl Loading

If fgop=1 Then
  "p (bar)| T (ºC) | "→txttle
Else
  "T (ºC)| p (bar) | "→txttle
EndIf

If propop=1 Then
  txttle&"vf     | vg (m3/kg)"→txttle
  {1,2,3,4}→taloc
  {6,6,8,8}→txtlm
  {"| ","| ","| "}→txtfmt
ElseIf propop=2 Then
  txttle&"uf     | ug (kJ/kg)"→txttle
  {1,2,5,6}→taloc
  {6,6,6,6}→txtlm
  {"| ","| ","| "}→txtfmt
ElseIf propop=3 Then
  txttle&"hf  |hfg  |hg(kJ/kg)"→txttle
  {1,2,7,8,9}→taloc
  {6,6,6,6,6}→txtlm
  {"| ","| ","| ","| "}→txtfmt
ElseIf propop=4 Then
  txttle&"sf  | sg (kJ/(kg.K))"→txttle
  {1,2,10,11}→taloc
  {6,6,6,6}→txtlm
  {"| ","| ","| "}→txtfmt
EndIf

combof(3,i1,i2,1,taloc,txtlm,txtfmt,tabela,fsz,jmax,0)
dim(resgener)+1→lpos
augment(resgener,seq("",i,lpos,8))→txtlist

Dialog
  Title "Esc p/trás. OK p/outro v,u,h,s."
  Text txttle
  Text txtlist[1]
  Text txtlist[2]
  Text txtlist[3]
  Text txtlist[4]
  Text txtlist[5]
  Text txtlist[6]
  Text txtlist[7]
  Text txtlist[8]
  DropDown "Listar qual propriedade?",proplist,propop
EndDlog
If Ok=1 Then
  Goto Loading
Else
  Goto Start
EndIf

Lbl Fim
setmode("Display Digits",cm)→cm

Refs_Tdl(TabNum,1)
Refs_Tdl(TabNum,2)

DispHome

eliminar("resgener")
EndPrgm