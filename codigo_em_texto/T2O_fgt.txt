()
Prgm

Local  ploc,plista,a2li1,a2li2,a3li1,a3li2,i,i1,i2,i3,taloc,tlista,tloc,txtlist,txttle,length,lpos,maxlist,fgop,propop,tmpval,txtlm,txtfmt,cm,tabela,proplist,nument,fsz,jmax

ClrIO
Disp "A iniciar..."
eliminar("resgener")
{"v","u","h","s"}»proplist
seq(string(i),i,1,8)»nument

h2o_xtc(2)
h2o_xtc(3)

Disp "10%"

setMode("Display Digits","Float 12")»cm
8»maxlist

exact(tabh2oa2[2,1])»a2li1
exact(tabh2oa2[4,1])»a2li2
combof(3,a2li1,a2li2,1,{1},{0},{""},"tabh2oa2",7834,71,0)
resgener»tlista

Disp "50%"

exact(tabh2oa3[2,1])»a3li1
exact(tabh2oa3[4,1])»a3li2
combof(3,a3li1,a3li2,1,{1},{0},{""},"tabh2oa3",5634,51,0)
resgener»plista

ClrIO

Lbl  start

Dialog
  Title  "Tabelas: Água saturada (A-2 ou A-3)"
  Text  " Escolha se quer a Pressão ou a Tempe_"
  Text  "ratura, o valor por onde comecar,"
  Text  "quantas entradas listar (max 8) e qual"
  Text  "propriedade deseja ver!"
  Text  ""
  DropDown  "Pressão\Temperatura?",{"Pressão","Temp."},fgop
  DropDown  "Pressão (bar)?",plista,ploc
  DropDown  "Temperatura (¬C)?",tlista,tloc
  DropDown  "Listar quantas entradas?",nument,maxlist
  DropDown  "Listar qual propriedade?",proplist,propop
EndDlog

If  ok=0
  Goto  fim

If fgop=1 Then

  a3li2-a3li1»i2
  If i2-ploc+1<maxlist Then
    i2-ploc+1»i2
  Else
    maxlist»i2
    i2-1»i2
  EndIf
  "tabh2oa3"»tabela
  5634»fsz
  51»jmax

Else

  a2li2-a2li1»i2
  If i2-tloc+1<maxlist Then
    i2-tloc+1»i2
  Else
    maxlist»i2
    i2-1»i2
  EndIf
  "tabh2oa2"»tabela
  7834»fsz
  71»jmax

EndIf

If fgop=1 Then
  a3li1+ploc-1»i1
Else
  a2li1+tloc-1»i1
EndIf

i1+i2»i2

Lbl  loading

If fgop=1 Then
  "p (bar)| T (¬C) | "»txttle
Else
  "T (¬C)| p (bar) | "»txttle
EndIf

If propop=1 Then
  txttle&"vf     | vg (m3/kg)"»txttle
  {1,2,3,4}»taloc
  {6,6,8,8}»txtlm
  {"| ","| ","| "}»txtfmt
ElseIf propop=2 Then
  txttle&"uf     | ug (kJ/kg)"»txttle
  {1,2,5,6}»taloc
  {6,6,6,6}»txtlm
  {"| ","| ","| "}»txtfmt
ElseIf propop=3 Then
  txttle&"hf  |hfg  |hg(kJ/kg)"»txttle
  {1,2,7,8,9}»taloc
  {6,6,6,6,6}»txtlm
  {"| ","| ","| ","| "}»txtfmt
ElseIf propop=4 Then
  txttle&"sf  | sg (kJ/(kg.K))"»txttle
  {1,2,10,11}»taloc
  {6,6,6,6}»txtlm
  {"| ","| ","| "}»txtfmt
EndIf

combof(3,i1,i2,1,taloc,txtlm,txtfmt,tabela,fsz,jmax,0)
dim(resgener)+1»lpos
augment(resgener,seq("",i,lpos,8))»txtlist

Dialog
  Title  "Esc p/trás. OK p/outro v,u,h,s."
  Text  txttle
  Text  txtlist[1]
  Text  txtlist[2]
  Text  txtlist[3]
  Text  txtlist[4]
  Text  txtlist[5]
  Text  txtlist[6]
  Text  txtlist[7]
  Text  txtlist[8]
  DropDown  "Listar qual propriedade?",proplist,propop
EndDlog
If ok=1 Then
  Goto  loading
Else
  Goto  start
EndIf

Lbl  fim
setMode("Display Digits",cm)»cm

h2o_tdel(2)
h2o_tdel(3)

DispHome

eliminar("resgener")
EndPrgm